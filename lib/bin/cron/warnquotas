#!/usr/bin/perl

use Passwd::Unix;
use Quota;

use Mail::Send;
use Storable;

my $statusfile = '/dreamhack/var/quotamsg.status';

my $threshold = 90;   # percent
my $status;
if (-f $statusfile) { $status = retrieve($statusfile); }
               else { $status = {}; }

my $pu = new Passwd::Unix();

foreach my $user ($pu->users) {
  next if ($user!~/^dh-/);   # we only care about Dreamhack people with this script
  my ($used, undef, $hard) = Quota::query(Quota::getqcarg("/"), $pu->uid($user), 0);
  next if ((!defined($used)) || ($hard == 0));
  if ($used < ($hard / 100 * $threshold)) {
    # they're below their quota.
    delete $status->{$user};
    next;
  }
  next if (defined($status->{$user}) && (($status->{$user} + (86400 * 7)) > time()));
  $status->{$user} = time();
  my $usedp = sprintf("%.2f", (($used / $hard) * 100));
  print "$user: used $used, hard $hard, $usedp% of quota\n";
  my $msg = new Mail::Send(
                            subject => "hack.dreamwidth.net quota warning ($usedp% used)",
                            to      => $user,
                            bcc     => 'sophie',
                          );
  $msg->set("From", '"Sophie" <sophie@hack.dreamwidth.net>');
  my $fh = $msg->open();
  my $hardm = sprintf("%.2f", ($hard * 1024 / 1048576));   # yes, I could do / 1024, but it's easier to understand this way.
  my $usedm = sprintf("%.2f", ($used * 1024 / 1048576));   # ditto.
  my $name = $pu->gecos($user);
  print $fh <<MSG;
[this is an automatic message]

Hi there, $name!

This is just an automatic message, sent out by a program that checks
quota limits, to let you know you're nearing your quota on
hack.dreamwidth.net. If you need more, no problem - just let me know
by replying to this message.

You currently have a limit of $hard blocks ($hardm MB). Of this, you're
using $used blocks ($usedm MB). That means your usage is $usedp% of the
limit right now.

You may want to delete some files to keep from getting to the limit, as
the limit is automatically enforced and the system will prevent you
from exceeding it. If you have any automatic scripts that rely on
writing files to the filesystem, they could fail unexpectedly.

This message will repeat weekly as long as you use more than $threshold% of
your quota. (However, if you go below this threshold one day and then
exceed it the next, this message will be sent out sooner.)

As a reminder, your username on hack.dreamwidth.net is $user.

Thanks! And again, if you need more quota, just let me know by replying
to this message.

 - Sophie.
MSG
  $fh->close();
}

store $status, $statusfile;

