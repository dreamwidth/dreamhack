#!/usr/bin/perl

use LWP::Simple;
use JSON;

my $gitname = <STDIN>;   # since this is going to run as root using user-supplied input, it's a bit more secure to
                         # insist that input comes from STDIN and not the command line.
chomp($gitname);

if ($gitname=~/(?:^-|[^a-zA-Z0-9\-])/) {
  print STDERR "GitHub usernames may only contain alphanumeric characters or dashes and cannot begin with a dash.\n";
  exit 4;
}

# check to see if the GitHub account exists
my $userout = get("https://api.github.com/users/$gitname");
if (!defined $userout) {
  print STDERR "GitHub account '$gitname' does not exist or an error occurred communicating with GitHub.\n";
  exit 1;
}
my $u = decode_json($userout);
my $canon = $u->{'login'};   # this is the canonically-cased name, necessary for setting up the repos.
print STDERR "GitHub user '$canon' found, checking repos...\n";

my @repos = qw(dw-free dw-nonfree);
foreach my $repo (@repos) {
  print STDERR "$canon/$repo... ";
  my $repoout = get("https://api.github.com/repos/$canon/$repo");
  if (!defined $repoout) {
    print STDERR "doesn't exist (or communication failure) :(\n";
    exit 2;
  }
  my $r = decode_json($repoout);
  # check that the owner matches
  if ($r->{'owner'}->{'id'} != $u->{'id'}) {
    print STDERR "exists, but isn't owned by $canon :(\n";
    exit 3;
  }
  print STDERR "exists :D\n";
}
print $canon;
